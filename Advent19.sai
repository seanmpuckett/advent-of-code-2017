
reference:
  FS require('fs')

object Advent19 1.0.0

Instantiate task

  set puzzle chain ~FS.readFileSync('Advent19.txt','utf8') 
    split /\r|\n/
    thru .split('')    

  set 
    y 0
    x puzzle[0] into 0 .='|' ?? number key :: sum
    compass 's'
    delta:
      n: 0, -1
      e: 1, 0
      s: 0, 1
      w: -1, 0
    path empty
    steps to -1
    done false
      
  until done
    switch puzzle[y][x]
      case ' '
        set done true
      case '+'
        if compass='n' or compass='s'
          set compass (puzzle[y][x-1] != ' ') ?? 'w' :: 'e'
        else
          set compass (puzzle[y-1][x] != ' ') ?? 'n' :: 's'
      case '|', '-'
        break
      default
        path.push trial
    inc steps
    with delta[compass]
      set
        x + .0
        y + .1

  debug "Part 1: ${path.join('')}"
  debug "Part 2: ${steps}"
